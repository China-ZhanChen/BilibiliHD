plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'com.google.protobuf'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-parse", "--short", "HEAD"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitCommitCount = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "rev-list", "dev", "--count"
        standardOutput = stdout
    }
    return stdout.toString().toInteger()
}

def isHasChange = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "status", "--short"
        standardOutput = stdout
    }
    return !stdout.toString().trim().isEmpty()
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.duzhaokun123.bilibilihd"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode getGitCommitCount()
        versionName "0.1-git.${getGitHash()}${isHasChange() ? "-changed" : ""}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("boolean", "IS_GIT_VERSION", "true")
        multiDexEnabled true
        resConfigs "en", "zh-rCN"
    }

    buildTypes {

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }

        debug {
            zipAlignEnabled false
        }

    }
    packagingOptions {
        pickFirst 'META-INF/ktor-http.kotlin_module'
        pickFirst 'META-INF/ktor-utils.kotlin_module'
        pickFirst 'META-INF/kotlinx-coroutines-core.kotlin_module'
        pickFirst 'META-INF/ktor-io.kotlin_module'
        pickFirst 'META-INF/ktor-http-cio.kotlin_module'
        pickFirst 'META-INF/ktor-client-core.kotlin_module'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding = true
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
    applicationVariants.all { variants ->
        variants.outputs.all {
            outputFileName = "${defaultConfig.applicationId}_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${buildType.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-rc1'
//    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.3.9'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    //browser
    implementation 'androidx.browser:browser:1.2.0'
    //bilibili-api
    implementation 'com.hiczp:bilibili-api:0.2.1'
//    DrawerLayout
    implementation 'androidx.drawerlayout:drawerlayout:1.1.0'
    //CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    implementation 'com.google.android.material:material:1.3.0-alpha02'
//    implementation 'androidx.navigation:navigation-fragment:2.2.1'
//    implementation 'androidx.navigation:navigation-ui:2.2.1'
//    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    //XRecyclerView
    implementation 'com.jcodecraeer:xrecyclerview:1.6.0'
//    implementation "androidx.cardview:cardview:1.0.0"
//    implementation "com.google.android.material:material:1.1.0"
    //PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha05'
    //okhttp3
    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    //gson
    implementation 'com.google.code.gson:gson:2.8.6'
    //exoplayer
    implementation 'com.google.android.exoplayer:exoplayer-core:2.11.3'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.11.3'
//    //mobile-ffmpeg
//    implementation 'com.arthenica:mobile-ffmpeg-min:4.3.1'
    //okdownload
    // core
//    implementation 'com.liulishuo.okdownload:okdownload:1.0.7'
//    // provide sqlite to store breakpoints
//    implementation 'com.liulishuo.okdownload:sqlite:1.0.7'
//    // provide okhttp to connect to backend
//    implementation 'com.liulishuo.okdownload:okhttp:1.0.7'
    //DanmakuFlameMaster
    implementation 'com.github.ctiao:DanmakuFlameMaster:0.9.25'
//    implementation 'com.github.ctiao:ndkbitmap-armv7a:0.9.25'
//    implementation 'com.github.ctiao:ndkbitmap-x86:0.9.25'
    //Preference
    implementation 'androidx.preference:preference:1.1.1'

    //lifecycle
    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0-alpha07'
    // LiveData
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0-alpha07'

    //MediaRouter
    implementation 'androidx.mediarouter:mediarouter:1.1.0'
    //protobuf-lite
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    //html-textview
    implementation 'org.sufficientlysecure:html-textview:3.9'
    //ktx
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    //flexbox-layout
    implementation 'com.google.android:flexbox:2.0.1'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.7.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}